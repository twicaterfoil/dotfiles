# shortcut to this dotfiles path is $ZSH

export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export DEVELOPER=~/Developer

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.sh)
config_files=(${config_files:#*/install.sh})

# load the path files first
for file in ${(M)config_files:#*/path.sh}
do
  source $file
done

# load the other files (everything but the path and completion files)
for file in ${${config_files:#*/path.sh}:#*/completion.sh}
do
  source $file
done

# link dotfiles bin
export PATH=$ZSH/bin:$PATH

# remove dupes from PATH
typeset -U PATH

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.sh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
